apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

description 'Strategy to load HOCON files'
group = 'com.github.ladutsko'
version = '1.0.0'

repositories {
    mavenLocal()
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext.versions = [
    springBoot: '1.5.9.RELEASE'
]

dependencies {
    compile 'com.typesafe:config:1.3.2'
    compile "org.springframework.boot:spring-boot:${versions.springBoot}"

    testCompile "org.springframework.boot:spring-boot-starter-test:${versions.springBoot}"
}

jar {
    into('META-INF') {
        from files('LICENSE')
    }

    manifest {
        attributes('Implementation-Title' : project.description,
                   'Implementation-Version' : project.version)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'https://github.com/ladutsko/spring-boot-starter-hocon'

                scm {
                    connection 'scm:git:git@github.com:ladutsko/spring-boot-starter-hocon.git'
                    developerConnection 'scm:git:git@github.com:ladutsko/spring-boot-starter-hocon.git'
                    url 'git@github.com:ladutsko/spring-boot-starter-hocon.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'ladutsko'
                        name 'George Ladutsko'
                        email 'ladutsko@gmail.com'
                    }
                }
            }
        }
    }
}
